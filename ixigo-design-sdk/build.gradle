plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
    id 'com.ncorti.ktfmt.gradle' version '0.7.0'
    id 'com.diffplug.spotless' version '6.0.5'
}

group = 'com.ixigo.sdk'
version = '1.0.0-alpha21'

android {
    compileSdkVersion 33
    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.2"
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionName "$version"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.ixigo.design.sdk'
}

dependencies {

    // Compose Material Design
    implementation 'androidx.compose.material:material:1.3.1'
    implementation "androidx.compose.material3:material3:1.0.1"
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.3.3'

    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.compose.ui:ui:1.3.3"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'com.google.android.material:material:1.7.0'

    implementation 'io.coil-kt:coil-compose:2.2.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            snapshot(MavenPublication) {
                from components.release
                artifactId = "${project.name}"
                artifact androidSourcesJar
                version = "${version}-SNAPSHOT"
            }
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                artifactId = "${project.name}"
            }
        }

        repositories {
            maven {
                url = 'https://nexus.ixigo.com/nexus/content/repositories/androidshared'
                name = 'Nexus'
                credentials {
                    username = findProperty('nexusUser') ?: System.env.nexusUser
                    password = findProperty('nexusPassword') ?: System.env.nexusPassword
                }
            }
            maven {
                url = 'https://nexus.ixigo.com/nexus/content/repositories/androidshared-snapshots'
                name = 'NexusSnapshot'
                credentials {
                    username = findProperty('nexusUser') ?: System.env.nexusUser
                    password = findProperty('nexusPassword') ?: System.env.nexusPassword
                }
            }
        }
    }
}
